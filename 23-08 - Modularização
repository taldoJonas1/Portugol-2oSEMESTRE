EXEMPLOS: 

programa
{
	
	funcao inicio()
	{
		inteiro a, b
		a = 7
		b = 15
		escreva(a, " ", b, "\n")
		troca(a, b)
		escreva(a, " ", b, "\n")
	}

	funcao troca(inteiro &x, inteiro &y){
		inteiro aux
		aux = x
		x = y
		y = aux
	}
}

= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = 

programa
{
	
	funcao inicio()
	{
		inteiro a, b, s
		a = 4
		b = 9
		s = soma(a, b)
		escreva("Soma = ", s, "\n")
	}
	
	funcao inteiro soma(inteiro x, inteiro y){
		inteiro result
		 result = x+y
		 retorne result
	}
}

==================================================================================================
EXERCÍCIOS:
1. VERIFICAR SE O NRO É DIVISIVEL POR 6 OU NAO

programa
{
	
	funcao inicio()
	{
		inteiro x
		cadeia y
		escreva("Insira o valor de x: ")
		leia(x)
		y = div(x)
		escreva(y)
	}
	funcao cadeia div(inteiro x){
		cadeia result
		se(x % 6 == 0){
			result = "É DIVISÍVEL POR 6"
		}
		senao{
			result = "NÃO É DIVISÍVEL POR 6"
		}
		retorne result
	}
}

==================================================================================================

2. CALCULAR O QUOCIENTE INTEIRO DA DIVISAO ENTRE DOIS NROS, SEM UTILIZAR OPERADOR DIV

programa
{
	
	funcao inicio()
	{
		inteiro A, B, N
		escreva("Insira o dividendo: ")
		leia(A)
		escreva("Agora insira o divisor: ")
		leia(B)
		N = div(A, B)
		escreva("Resultado da divisão sem o resto: ",N)
	}

	funcao inteiro div(inteiro A, inteiro B){
		inteiro cont=0
		enquanto(A>=B){
			cont++
			A = A - B
		}
		retorne cont
	}
}

==================================================================================================

3. CALCULAR O FATORIAL DE UM NRO

programa
{
	
	funcao inicio()
	{
		inteiro N, X
		escreva("Entre com um nro: ")
		leia(N)
		X = fat(N)
		escreva(N,"! = ", X)
	}
	funcao inteiro fat(inteiro N){
		inteiro fatorial = 1
		enquanto(N>0){
		fatorial = fatorial * N
		N--
		}
		retorne fatorial
	}
}

==================================================================================================

4. Fazer um subprograma que receba, por parâmetro, a hora de início e a hora de término de um jogo, ambas subdivididas
em 2 valores distintos: horas e minutos. O subprograma deve retornar a duração do jogo em minutos, considerando que o 
tempo máximo de duração de um jogo é de 24 horas e que o jogo pode começar em um dia e terminar no outro.

programa
{
	inclua biblioteca Util --> u
	
	funcao inicio()
	{
		inteiro h1, h2, min1, min2, dur
		h1 = u.sorteia(1, 4)
		min1 = u.sorteia(0, 59)
		escreva("\nHORA DE INÍCIO\n", h1,":",min1)
		h2 = u.sorteia(5, 8)
		min2 = u.sorteia(0, 59)
		escreva("\n\nHORA DE TÉRMINO\n", h2,":",min2)
		dur = duracao(h1, h2, min1, min2)
		escreva("\n\nO jogo durou um total de ", dur," minutos.")
	}
	funcao inteiro duracao(inteiro h1, inteiro h2, inteiro min1, inteiro min2){
		inteiro i = 0, t = 0, d
		i = h1*60 + min1
		t = h2*60 + min2
		d = t - i
		retorne d
	}
}
